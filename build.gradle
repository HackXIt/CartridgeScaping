plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version('0.0.10')
    id 'org.beryx.jlink' version('2.24.1')
    id 'org.javamodularity.moduleplugin' version('1.8.8')
}

group('fhtw')
version('0.1-MVP')

// Auto accept terms of service when performing build-scan
if (hasProperty('buildScan')) {
/*
    FIXME For some reason this variable gives: "No candidates found for method call buildScan"
    NOTE It runs just fine however...
 */
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

repositories {
    mavenCentral()
}

//sourceCompatibility = '17'
//targetCompatibility = '17'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
tasks.named('wrapper') {
    distributionUrl = 'https://services.gradle.org/distributions/gradle-7.3.3-all.zip'
}

application {
    mainModule = 'fhtw.cartridgeScaping'
    mainClass = 'fhtw.cartridgeScaping.CartridgeScapingApp'
}

javafx {
    version = '17.0.1'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    testImplementation('org.testng:testng:7.5')
}

test {
    useTestNG()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip") as RegularFile
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'CartridgeScapingApp'
    }
}

jlinkZip {
    group = 'distribution'
}